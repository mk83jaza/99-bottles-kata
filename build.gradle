plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'info.solidsoft.pitest' version '1.7.4'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {

    testImplementation(platform("org.junit:junit-bom:5.10.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.assertj:assertj-core:3.22.0")
    testImplementation("com.approvaltests:approvaltests:23.0.1")
    testImplementation("com.larseckart:junit-tcr-extensions:0.0.1")
    testImplementation("org.mockito:mockito-core:5.11.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.11.0")
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-Xlint:all")
    options.incremental = true
}


application {
    mainClass.set("ferry.booking.Program")
}

jar {
    manifest {
        attributes "Main-Class": "ferry.booking.Program"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

project.tasks.check.dependsOn("pitest")
pitest {
    junit5PluginVersion = '0.15'
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    targetClasses = ['ferry.booking.*']
    pitestVersion = '1.7.0'
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}
